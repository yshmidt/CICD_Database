
-- =============================================
-- Author:			Debbie
-- Create date:		02/11/2020
-- Description:		Created for Manufacture Variance Detail.  Many clients were requesting how to find out what detailed records was contributing to the manufacture Variance transactions generated by manex. 
--					by creating this quickview it should be able to give the end users enough information to do their own research without having to request assistance from manex.   
-- Reports:			
-- Modifications:   
-- 03/28/19 VL If the KIT is never put in proocess, then should not be apper in kit close report, filter out KitStauts=0 records.  Zendesk#3240
-- 02/11/20 DRP:  Took the rptKitToCloseDetail procedure and made modifications to list out the individual part number information that pertained to the Mfgr Variance.  
--				  Also remove the KitSTatus filter.  By removing the filter it will allow the users to run this quickview for either Closed or not closed Kits. 
-- 02/14/20 VL added next line, we only can show kit not closed records.  Because once the KIT is closed, the inventor.stdcost might be changed after that, then the mfgr variance calculation won't be correct.
-- 02/14/20 VL Changed @RollupCost and @IssueCost structure for cube
-- 02/17/20 VL Get the parts that are not in BOM, e.g. line short
-- =============================================
 CREATE PROCEDURE [dbo].[rptMfgrVarWoNoDetail]
--DECLARE

 @lcWoNo char(10) = ''
 ,@userId uniqueidentifier=null 

as 
Begin

SET @lcWono=dbo.PADL(@lcWono,10,'0')

DECLARE @nComplete AS numeric(7,0), @cWono AS char(10), @lnTotalNo int, @lnCount int, @nRollupCost numeric(20,7), @nIssuCost numeric(20,7),
		@cPart_no char(25), @cRevision char(8), @cDescript char(45), @dComplDate smalldatetime, @cCustName char(35), @cSono char(10),
		@cPart_class char(8), @cPart_type char(8)

declare @WoNotCl as table	(nrecno int identity, wono char(10),complete numeric (13,0),Due_Date smalldatetime,SONO char(10),UNIQ_KEY char(10)
							,PART_NO char(35),REVISION char(8),DESCRIPT char(45),PART_CLASS char(8),PART_TYPE char(8),CUSTNAME char(35)
							,USESETSCRP bit,STDBLDQTY numeric(13,0),BLDQTY numeric(13,0))


--I created a table that will be populated with the info from the procedure
declare @RollupCost as table	(Uniq_key char(10), Part_Sourc char(8), StdCost numeric(13,5),
			Qty numeric(12,2), U_of_meas char(4), Scrap numeric(6,2), SetupScrap numeric(4,0), Phant_Make bit,
			UniqBomNo char(10), Ext_cost numeric(25,5), SetupScrap_Cost numeric(13,5), Ext_cost_total numeric(25,5),
			QtyReqTotal numeric(16,2), StdBldQty numeric(8,0), Ext_costWithoutCEILING numeric(13,5),
			QtyReqWithoutCEILING numeric(16,2), Ext_cost_totalWithoutCEILING numeric(25,5), QtyReqTotalWithoutCEILING numeric(16,2),
			-- 04/07/17 VL added functional currency code
			StdCostPR numeric(13,5), Ext_costPR numeric(25,5), SetupScrap_CostPR numeric(13,5), Ext_cost_totalPR numeric(25,5),
			Ext_costWithoutCEILINGPR numeric(13,5), Ext_cost_totalWithoutCEILINGPR numeric(25,5))


-- 01/18/13 VL created issue cost cursor
DECLARE @IssueCost AS TABLE		(Uniq_key char(10), Qtyisu numeric(12,2), OldUnitCost numeric(13,5), OldCost numeric(20,7),  
							NewUnitCost numeric(13,5), Part_Sourc char(10), NewCost numeric(20,7), OldUnitCostPR numeric(13,5), OldCostPR numeric(20,7), NewUnitCostPR numeric(13,5), NewCostPR numeric(20,7))


declare @cUniq_key AS char(10) 
		, @dDue_Date AS smalldatetime
		, @nStdBldQty numeric (8,0) 
		, @nBldQty AS numeric(7,0)

insert into @WoNotCl 							
	select	WOENTRY.WONO,WOENTRY.COMPLETE,WOENTRY.DUE_DATE,WOENTRY.SONO,INVENTOR.UNIQ_KEY,INVENTOR.PART_NO,INVENTOR.REVISION,INVENTOR.DESCRIPT,INVENTOR.PART_CLASS
			,INVENTOR.PART_TYPE,CUSTOMER.CUSTNAME,INVENTOR.USESETSCRP,iNVENTOR.STDBLDQTY,WOENTRY.BLDQTY
	from	WOENTRY
			INNER JOIN INVENTOR ON WOENTRY.UNIQ_KEY = INVENTOR.UNIQ_KEY
			INNER JOIN CUSTOMER ON WOENTRY.CUSTNO = CUSTOMER.CUSTNO
	where	woentry.OPENCLOS = 'Closed'
			-- 02/14/20 VL added next line, we only can show kit not closed records.  Because once the KIT is closed, the inventor.stdcost might be changed after that, then the mfgr variance calculation won't be correct.
			and woentry.KITSTATUS <> 'KIT CLOSED'
			-- 03/28/19 VL If the KIT is never put in proocess, then should not be apper in kit close report, filter out KitStauts=0 records.  Zendesk#3240
			AND KitStatus <> ''
			and @lcWoNo = wono


SET @lnTotalNo = @@ROWCOUNT;
	
IF (@lnTotalNo>0)
BEGIN	
	SET @lnCount=0;
	WHILE @lnTotalNo>@lnCount
	BEGIN	
		SET @lnCount=@lnCount+1;
		--below I was attempting to populate the parameter with the information from the @WoNotCl table above		
		SELECT @cUniq_key = Uniq_key, @dDue_Date = Due_Date, @nStdBldQty = STDBLDQTY,@nBldQty = BLDQTY, 
				@cWono = Wono, @nComplete = Complete, @cDescript = DESCRIPT, 
				@cCustName = CUSTNAME, @cSono = Sono, @cPart_class = Part_class, @cPart_type = Part_type, @cPart_no = PART_NO, @cRevision = Revision
			FROM @WoNotCl AS W1
			WHERE nrecno = @lnCount
		IF (@@ROWCOUNT<>0)
		BEGIN
			--delete all records if exist
			DELETE FROM @RollupCost WHERE 1=1
			DELETE FROM @IssueCost WHERE 1=1
			
			INSERT @RollupCost EXEC sp_RollupCost @cUniq_key, @dDue_Date,@nStdBldQty,@nBldQty
			
			
			INSERT @IssueCost EXEC sp_IssuUpCost_IncludeOverIsu @cWono

; with Z1 as (
				select	w1.Wono,w1.complete,w1.Part_no as ProdNo,W1.Revision as ProdRev,w1.DESCRIPT,w1.Custname,w1.USESETSCRP,w1.STDBLDQTY,w1.BLDQTY
						,inventor.Part_no,inventor.Revision,Inventor.Uniq_key,Inventor.Part_sourc,ISNULL(Qtyisu,0) as Qty,ISNULL(NewUnitCost,0) as IssuedUnitCost, ISNULL(newcost,0) as IssuedCost
						,ISNULL(CASE WHEN ROW_NUMBER() OVER(Partition by UNIQBOMNO Order by UNIQBOMNO)=1 Then R.Ext_Cost_totalWithoutCEILING ELSE CAST(0.00 as Numeric(20,2)) END,0)  AS BomCost
						,ISNULL(CASE WHEN ROW_NUMBER() OVER(Partition by UNIQBOMNO Order by UNIQBOMNO)=1 Then R.SetupScrap_Cost ELSE CAST(0.00 as Numeric(20,2)) END,0)  AS SetupScrap_Cost
						,R.UniqBomNo
				from	@IssueCost as I	
						--inner join INVENTOR on I.Uniq_key = inventor.UNIQ_KEY
						RIGHT OUTER JOIN @RollupCost R ON I.UNIQ_KEY = R.uniq_key
						inner join INVENTOR on R.Uniq_key = inventor.UNIQ_KEY
						cross apply @WoNotCl as W1
				UNION ALL
				-- 02/17/20 VL Get the parts that are not in BOM, e.g. line short
				select	w1.Wono,w1.complete,w1.Part_no as ProdNo,W1.Revision as ProdRev,w1.DESCRIPT,w1.Custname,w1.USESETSCRP,w1.STDBLDQTY,w1.BLDQTY
						,inventor.Part_no,inventor.Revision,I.Uniq_key,I.Part_sourc,Qtyisu as Qty,NewUnitCost as IssuedUnitCost,newcost as IssuedCost
						,0 AS BomCost
						,0 AS SetupScrap_Cost
						,'' AS UniqBomNo
				from	@IssueCost as I	
						inner join INVENTOR on I.Uniq_key = inventor.UNIQ_KEY
						cross apply @WoNotCl as W1
				WHERE NOT EXISTS(SELECT 1 FROM @RollupCost R WHERE R.uniq_key = I.Uniq_key)
			)

select	Wono,complete,ProdNo,ProdRev,DESCRIPT,Custname,USESETSCRP,STDBLDQTY,BLDQTY,Part_no,Revision,Uniq_key,Part_sourc
		,sum(Qty) as TotalIssueQty,IssuedUnitCost,sum(IssuedCost) as TotalIssuedCost,sum(BomCost) as BomCost
		,sum(SetupScrap_Cost) as SetupScrap_cost,UniqBomNo,sum(IssuedCost) - sum(BomCost) as Variance
from	z1
Group by Wono,complete,ProdNo,ProdRev,DESCRIPT,Custname,USESETSCRP,STDBLDQTY,BLDQTY,Part_no,Revision,Uniq_key,Part_sourc
		,IssuedUnitCost,UniqBomNo

		end
		end
		end
end